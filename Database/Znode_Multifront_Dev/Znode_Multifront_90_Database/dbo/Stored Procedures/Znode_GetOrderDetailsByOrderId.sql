CREATE PROCEDURE [dbo].[Znode_GetOrderDetailsByOrderId]
(
	@OmsOrderId	INT
)
AS
/* 
	Summary: This SP is used to get Order Details using OrderId
	EXEC Znode_GetOrderDetailsByOrderId @OmsOrderId = 105557
*/
BEGIN
BEGIN TRY
    SET NOCOUNT ON;

	SELECT OmsOrderId,OrderNumber,IsQuoteOrder,OmsQuoteId
	FROM ZnodeOmsOrder WHERE OmsOrderId = @OmsOrderId

	SELECT OrderDetail.OmsOrderDetailsId, OrderDetail.OmsOrderId, OrderDetail.PortalId AS PortalId, 
		OrderDetail.UserId, OrderDetail.OrderDate, OrderDetail.OmsOrderStateId, OrderDetail.ShippingId, 
		OrderDetail.PaymentTypeId, OrderDetail.BillingFirstName, OrderDetail.BillingLastName, OrderDetail.BillingCompanyName, 
		OrderDetail.BillingStreet1, OrderDetail.BillingStreet2, OrderDetail.BillingCity, OrderDetail.BillingStateCode, 
		OrderDetail.BillingPostalCode, OrderDetail.BillingCountry, OrderDetail.BillingPhoneNumber, OrderDetail.BillingEmailId, 
		OrderDetail.TaxCost, OrderDetail.ShippingCost, OrderDetail.SubTotal, OrderDetail.DiscountAmount, OrderDetail.CurrencyCode, 
		OrderDetail.OverDueAmount, OrderDetail.Total, OrderDetail.ShippingNumber, OrderDetail.TrackingNumber, OrderDetail.CouponCode, 
		OrderDetail.PromoDescription, OrderDetail.ReferralUserId, OrderDetail.PurchaseOrderNumber, OrderDetail.OmsPaymentStateId, 
		OrderDetail.WebServiceDownloadDate, OrderDetail.PaymentSettingId, OrderDetail.PaymentTransactionToken, OrderDetail.ShipDate, 
		OrderDetail.ReturnDate, OrderDetail.AddressId, OrderDetail.PoDocument, OrderDetail.IsActive, OrderDetail.ExternalId, 
		OrderDetail.CreatedBy, OrderDetail.CreatedDate, OrderDetail.ModifiedBy, OrderDetail.ModifiedDate, OrderDetail.CreditCardNumber, 
		OrderDetail.IsShippingCostEdited, OrderDetail.IsTaxCostEdited, OrderDetail.ShippingDifference, OrderDetail.EstimateShippingCost, 
		OrderDetail.TransactionId, OrderDetail.Custom1, OrderDetail.Custom2, OrderDetail.Custom3, OrderDetail.Custom4, OrderDetail.Custom5, 
		OrderDetail.FirstName, OrderDetail.LastName, OrderDetail.CardType,  OrderDetail.CreditCardExpMonth, OrderDetail.CreditCardExpYear, 
		OrderDetail.TotalAdditionalCost, OrderDetail.PaymentDisplayName, OrderDetail.PaymentExternalId, OrderDetail.CultureCode, 
		OrderDetail.DisplayName, OrderDetail.Email, OrderDetail.PhoneNumber, OrderDetail.ShippingHandlingCharges, 
		OrderDetail.OrderTotalWithoutVoucher, OrderDetail.InHandDate, OrderDetail.ShippingConstraintCode, OrderDetail.JobName, OrderDetail.ShippingDiscount
	FROM  dbo.ZnodeOmsOrderDetails AS OrderDetail
	WHERE OrderDetail.OmsOrderId = @OmsOrderId

	SELECT Ship.ShippingId, Ship.ShippingTypeId, Ship.CurrencyId, 
		Ship.ShippingCode, Ship.ShippingName, Ship.HandlingCharge, Ship.HandlingChargeBasedOn, Ship.DestinationCountryCode, Ship.StateCode, 
		Ship.CountyFIPS, Ship.TrackingUrl, Ship.Description, Ship.IsActive, Ship.DisplayOrder, Ship.ZipCode, Ship.CreatedBy, Ship.CreatedDate, 
		Ship.ModifiedBy, Ship.ModifiedDate, Ship.DeliveryTimeframe, Ship.CultureId
	FROM ZnodeShipping Ship 
	WHERE EXISTS(SELECT * FROM ZnodeOmsOrderDetails OrderDetail WHERE OrderDetail.ShippingId = Ship.ShippingId AND OrderDetail.OmsOrderId = @OmsOrderId)


	SELECT OrderLine.OmsOrderLineItemsId, OrderLine.ParentOmsOrderLineItemsId, OrderLine.OrderLineItemRelationshipTypeId, 
		OrderLine.OmsOrderDetailsId, OrderLine.OmsOrderShipmentId, OrderLine.RmaReasonForReturnId, OrderLine.Sku, OrderLine.ProductName, 
		OrderLine.Description, OrderLine.Quantity, OrderLine.Price, OrderLine.Weight, OrderLine.DownloadLink, OrderLine.DiscountAmount, 
		OrderLine.ShipSeparately, OrderLine.ShipDate, OrderLine.ReturnDate, OrderLine.ShippingCost, OrderLine.PromoDescription, 
		OrderLine.TransactionNumber, OrderLine.PaymentStatusId, OrderLine.TrackingNumber, OrderLine.IsAutoGeneratedTracking, 
		OrderLine.OrderLineItemStateId, OrderLine.IsRecurringBilling, OrderLine.RecurringBillingPeriod, OrderLine.RecurringBillingCycles, 
		OrderLine.RecurringBillingFrequency, OrderLine.RecurringBillingAmount, OrderLine.AppliedPromo, OrderLine.CouponsApplied, 
		OrderLine.ExternalId, OrderLine.IsActive, OrderLine.CreatedBy, OrderLine.CreatedDate, OrderLine.ModifiedBy, OrderLine.ModifiedDate, 
		OrderLine.AutoAddonSKU, OrderLine.IsShippingReturn, OrderLine.PartialRefundAmount, OrderLine.Custom1, OrderLine.Custom2, OrderLine.Custom3, 
		OrderLine.Custom4, OrderLine.Custom5, OrderLine.GroupId
    FROM  dbo.ZnodeOmsOrderLineItems AS OrderLine
    WHERE Exists(select * from ZnodeOmsOrderDetails OrderDetail Where OrderLine.OmsOrderDetailsId = OrderDetail.OmsOrderDetailsId 
	     AND OrderDetail.IsActive = 1 AND OrderDetail.OmsOrderId = @OmsOrderId) 
	AND OrderLine.IsActive = 1
    ORDER BY OrderLine.OmsOrderLineItemsId ASC

	SELECT OrderState.OmsOrderStateId, OrderState.OrderStateName, OrderState.IsShowToCustomer, 
		OrderState.IsAccountStatus, OrderState.DisplayOrder, OrderState.Description, OrderState.IsEdit, OrderState.IsSendEmail, OrderState.IsOrderState, 
		OrderState.IsOrderLineItemState, OrderState.CreatedBy, OrderState.CreatedDate, OrderState.ModifiedBy, OrderState.ModifiedDate
	FROM dbo.ZnodeOmsOrderState AS OrderState
	WHERE EXISTS(SELECT * FROM dbo.ZnodeOmsOrderLineItems AS OrderLine WHERE OrderLine.IsActive = 1 AND 
				 Exists(select * from ZnodeOmsOrderDetails OrderDetail Where OrderLine.OmsOrderDetailsId = OrderDetail.OmsOrderDetailsId 
				 AND OrderDetail.IsActive = 1 AND OrderDetail.OmsOrderId = @OmsOrderId) 
				 AND OrderLine.OrderLineItemStateId = OrderState.OmsOrderStateId)
END TRY
BEGIN CATCH
	DECLARE @ERROR_PROCEDURE VARCHAR(1000)= ERROR_PROCEDURE(), @ErrorMessage NVARCHAR(MAX)= ERROR_MESSAGE(), @ErrorLine VARCHAR(100)= ERROR_LINE(), @ErrorCall NVARCHAR(MAX)= 'EXEC Znode_GetOrderDetailsByOrderId @OmsOrderId='+cast(@OmsOrderId as varchar(10));
	EXEC Znode_InsertProcedureErrorLog
	@ProcedureName    = 'Znode_GetOrderDetailsByOrderId',
	@ErrorInProcedure = @ERROR_PROCEDURE,
	@ErrorMessage     = @ErrorMessage,
	@ErrorLine        = @ErrorLine,
	@ErrorCall        = @ErrorCall;
END CATCH;
END